// This should give you a cute death message:
// FunkScene.restore ([9, [0,"name","Best Beloved"], 6, 0, 5, 0, 1, 0, 5, 0, 0])

amps = "Several"
dial = "dial"
hair = "messy"
name = "Adventurer"

#PAGE start
#(
#// Test comment inside scene text
#TITLE The Temple of Belsidore #ENDTITLE
# Temple of Belsidore
You, #$name, stand before the gates of the Temple of Belsidore. Beside the gate is a #[dial#].
#BUTTON Do it! #ENDBUTTON
#INCLUDE messy_hair
#/* Test multi-line comment in scene text...
  * ...still testing...
  * ...end multi-line comment */
#CYCLE(wind) The wind sighs.
#NEXT A light rain spatters.
#NEXT You feel a chill.
#LOOP
#CHOOSE I smash the gates! #FOR electrified
#SECRETLY #IF amps == "Several"
 #CHOOSE I turn the dial to 11! #FOR #( You turn the dial all the way to eleven. #{ amps="Eleven"; dial="dial, turned to eleven" #} #GOTO start #)
#IF 0 #CHOOSE I meditate contemplatively before acting #FOR wise_choice
#CHOOSE What is the philological origin of "Belsidore"? #FOR #( NERD!!! #OVER #)
#ROTATE(suicide_method)
 #CHOOSE I kill myself #FOR #( Done. #OVER #)
 #CHOOSE I end it all #FOR #( You got it. #OVER #)
 #CHOOSE I jump off a ledge #FOR #( Bye. #OVER #)
 #LOOP
#ONCE #CHOOSE I think of the good times. #FOR #( You think about all the good times. It doesn't take long. #GOTO #PREVIOUS #)
#AS penance #CHOOSE As penance, I cross myself. #FOR #( You cross yourself, in penance. #BACK #)
#AS penance #CHOOSE As penance, I regurgitate my last meal. #FOR #( You retch, purging yourself of evil. #BACK #)
#SECRETLY #IF 0 #CHOOSE I do nothing #FOR wise_choice
#CHOOSE I walk away #FOR wise_choice
#ONCE #CHOOSE I have a name, you know. #FOR #( #TITLE Getting acquainted... #ENDTITLE OK then, tell me your name. #INPUT Type your name: #TO name #BACK #)
#SECRETLY #IF wind == 2 #CHOOSE I pull my cloak tighter around me. #FOR #( Wimp. #ACHIEVE penance #CHOOSE Sorry. #FOR #PREVIOUS #)
#CHOOSE I brush my hair. #FOR #( You preen. #{ hair = "neat" #} #GOTO #PREVIOUS #)
#)

#PAGE electrified
#( #[ amps #] amps flow through your body.
Think that doesn't sound like a lot, #$name? No, you don't think that, because you're dead.
#OVER
#)

#PAGE wise_choice
#( A wise choice, my friend.

Now, I have to warn you that there's a bug coming up soon.
Without bugs there would be no point in a debugger; I hope you understand.

Are you ready for a bug?

#CHOOSE I'm ready! #FOR pre_bug_page
#CHOOSE No, get me away from that thing #FOR #( Very well. You live a tranquil bug-free existence to the end of your days. #)
#)

#PAGE pre_bug_page
#( I'm proud of you!

So, the bug's coming right up. Click on the button to get there.
#GOTO bug_page
#)

#PAGE bug_page
#(
Here's the bug! (You'll never see this text, I think.)
#GOTO hell
#)

#PAGE messy_hair
#( Your hair is very #$hair.
#)

#PAGE statusPage
#(
Your name is #$name.
#BAR Skill #VALUE 0 #MAX 10 #COLOR gray #ENDBAR
#BAR Experience #VALUE .1 #COLOR pink #ENDBAR
#BAR Luck #VALUE .5 #COLOR red #ENDBAR
#BAR Charisma #VALUE .7 #COLOR yellow #ENDBAR
#BAR Dexterity #VALUE .9 #COLOR orange #ENDBAR
#BAR Health #VALUE 1 #COLOR green #ENDBAR
#BAR Amps #VALUE amps=="Several" ? 3 : 11 #MAX 11 #COLOR purple #ENDBAR
#SHOW despair #BADGE You are penitent. #IF #ACHIEVED penance #NOW
#SHOW raining #BADGE You are damp. #IF wind==1 #EVER
#SHOW frankenstein-creature #BADGE #INCLUDE messy_hair #NOW
#)
