// This should give you a cute death message:
// funkscene.restore([9,[0,"name","Best Beloved"],6, 0, 5, 0, 1, 0, 5, 0, 0])

amps = "Several"
dial = "dial"
hair = "messy"
name = "Adventurer"

#PAGE start
#SCENE You, #$name, stand before the gates of the Temple of Belsidore. Beside the gate is a #[dial#].
#CYCLE(wind) The wind sighs.
#NEXT A light rain spatters.
#NEXT You feel a chill.
#LOOP
#CHOOSE I smash the gates! #FOR electrified
#SECRETLY #IF amps == "Several"
 #CHOOSE I turn the dial to 11! #FOR #( You turn the dial all the way to eleven. #{ amps="Eleven"; dial="dial, turned to eleven" #} #GOTO start #)
#IF 0 #CHOOSE I meditate contemplatively before acting #FOR wise_choice
#CHOOSE What is the philological origin of "Belsidore"? #FOR #SCENE NERD!!! #ENDSCENE
#CHOOSE I kill myself #FOR #( Done. #)
#ONCE #CHOOSE I think of the good times. #FOR #( You think about all the good times. It doesn't take long. #GOTO #PREVIOUS #)
#AS penance #CHOOSE As penance, I cross myself. #FOR #( You cross yourself, in penance. #BACK #)
#AS penance #CHOOSE As penance, I regurgitate my last meal. #FOR #( You retch, purging yourself of evil. #BACK #)
#SECRETLY #IF 0 #CHOOSE I do nothing #FOR wise_choice
#CHOOSE I walk away #FOR wise_choice
#ONCE #CHOOSE I have a name, you know. #FOR #( OK then, tell me your name. #INPUT Type your name: #TO name #BACK #)
#SECRETLY #IF wind == 2 #CHOOSE I pull my cloak tighter around me. #FOR #( Wimp. #BACK #)
#APPEND messy_hair
#ENDSCENE

#PAGE electrified
#SCENE #[ amps #] amps flow through your body.
Think that doesn't sound like a lot, #$name? No, you don't think that, because you're dead.
#ENDSCENE

#PAGE wise_choice
#SCENE A wise choice, my friend.
#ENDSCENE

#PAGE messy_hair
#SCENE Your hair is very #$hair.
#CHOOSE I brush my hair. #FOR #( You preen. #{ hair = "neat" #} #GOTO #PREVIOUS #)
#END
