// A little test FunkScene scene file, that trivially exercises many of the language features.

// This should give you a cute death message if you enter it at the console from the first page:
// FunkScene.restore ([9, [0,"name","Best Beloved"], 6, 0, 5, 0, 1, 0, 5, 0, 0])


// OK, so we are in the outermost JavaScript scope right now.
// Let's set some global variables:
amps = "Several"
dial = "dial"
hair = "messy"
name = "Adventurer"

// Define the start page, using FunkScene macros.
// The start page doesn't have to be the first scene declared, as long as it's called "start".
// You can also do something like
//   start = myFirstScene
// where myFirstScene is defined elsewhere.
// First, we use ##PAGE to name the scene
// (note that I have to escape ##PAGE with a double ##, even in a JavaScript comment;
//  the parser is not quite smart enough to avoid keywords inside JS comments yet)
#PAGE start
#// Now follows the definition of the scene.
#// Note the hash symbol that prefixes comments now,
#// since we are inside the "scene text",
#// where the default is to get out of the writer's way.

#// The following "#(" is the opening delimiter of the scene:
#(

#// Change the window title
#TITLE The Temple of Belsidore #ENDTITLE

#// Print a header and then some text (we are in Markdown mode, now)
# Temple of Belsidore
You, #$name, stand before the gates of the Temple of Belsidore. Beside the gate is a #[dial#].
#// Note the two different ways of embedding a variable in the above line:
#// #$name can only be used with a variable name
#// #[dial]# evaluates any arbitrary JavaScript expression

#// Change the text of the "Continue" button:
#BUTTON Do it! #ENDBUTTON

#// Include another scene inside this one:
#INCLUDE messy_hair

#/* Test multi-line comment in scene text...
  * ...still testing...
  * ...end multi-line comment */

#// OK, now some text that cycles between three different options,
#// with an index variable (wind) that allows us to keep track/
#CYCLE(wind) The wind sighs.
#NEXT A light rain spatters.
#NEXT You feel a chill.
#LOOP

#// OK, time for some choices!
#// These all have the basic form "#CHOOSE x #FOR y",
#// although there are some modifier keywords.

#// A straightforward choice that leads to another "page" (i.e. named scene):
#CHOOSE I smash the gates! #FOR electrified

#// A choice that only appears while the variable 'amps' holds the string value "Several":
#SECRETLY #IF amps == "Several"
 #CHOOSE I turn the dial to 11! #FOR #( You turn the dial all the way to eleven. #{ amps="Eleven"; dial="dial, turned to eleven" #} #GOTO start #)

#// A choice that will always be greyed out:
#IF false #CHOOSE I meditate contemplatively before acting #FOR wise_choice

#// A choice that leads immediately to a death scene:
#CHOOSE What is the philological origin of "Belsidore"? #FOR #( NERD!!! #OVER #)

#// A choice that rotates through several different options
#// Currently there can only be one option per rotation,
#// but the #NEXT separator allows for the future possibility of
#// different numbers of choices on each rotation:
#ROTATE(suicide_method)
 #CHOOSE I kill myself #FOR #( Done. #OVER #)
#NEXT
 #CHOOSE I end it all #FOR #( You got it. #OVER #)
#NEXT
 #CHOOSE I jump off a ledge #FOR #( Bye. #OVER #)
#LOOP

#// A choice that you can only make once:
#ONCE #CHOOSE I think of the good times. #FOR #( You think about all the good times. It doesn't take long. #GOTO #PREVIOUS #)

#// A pair of choices. When either one is taken, they both disappear, and the 'penance' event is recorded:
#AS penance #CHOOSE As penance, I cross myself. #FOR #( You cross yourself, in penance. #BACK #)
#AS penance #CHOOSE As penance, I regurgitate my last meal. #FOR #( You retch, purging yourself of evil. #BACK #)

#// A choice that will never appear:
#SECRETLY #IF 0 #CHOOSE I do nothing #FOR wise_choice

#// Another straightforward GOTO:
#CHOOSE I walk away #FOR wise_choice

#// Input of a variable:
#ONCE #CHOOSE I have a name, you know. #FOR
 #( #TITLE Getting acquainted... #ENDTITLE
    OK then, tell me your name.
#// Here is where the input happens:
    #INPUT Type your name: #TO name
    #BACK #)

#// Referencing an earlier CYCLE variable:
#SECRETLY #IF wind == 2 #CHOOSE I pull my cloak tighter around me. #FOR #( Wimp. #ACHIEVE penance #CHOOSE Sorry. #FOR #PREVIOUS #)

#// Executing code inside an anonymous scene:
#CHOOSE I brush my hair. #FOR #( You preen. #{ hair = "neat" #} #GOTO #PREVIOUS #)

#// OK, end of the start scene.
#)


#// Other scenes....
#// (NB we are back in JavaScript now, and yet I can still use FunkScene comments! Did I blow your MIND??)
// (I can use JS comments as well, woohoo)

#// First, DEATH:

#PAGE electrified
#( #[ amps #] amps flow through your body.
Think that doesn't sound like a lot, #$name? No, you don't think that, because you're dead.
#OVER
#)


#// If you run away, you encounter a bug:

#PAGE wise_choice
#( A wise choice, my friend.

Now, I have to warn you that there's a bug coming up soon.
Without bugs there would be no point in a debugger; I hope you understand.

Are you ready for a bug?

#CHOOSE I'm ready! #FOR pre_bug_page
#CHOOSE No, get me away from that thing #FOR
 #( Very well. You live a tranquil bug-free existence to the end of your days. #) #)

#PAGE pre_bug_page
#( I'm proud of you!

So, the bug's coming right up. Click on the button to get there.
#GOTO bug_page
#)

#PAGE bug_page
#(
Here's the bug! (You'll never see this text, I think.)
#GOTO hell
#)

#PAGE messy_hair
#( Your hair is very #$hair.
#)


#// The following is the status page.
#// It uses some cryptic and largely undocumented syntax,
#// which basically just sets up stats bars and accomplishment icons:

#PAGE statusPage
#(
Your name is #$name.
#BAR Skill #VALUE 0 #MAX 10 #COLOR gray #ENDBAR
#BAR Experience #VALUE .1 #COLOR pink #ENDBAR
#BAR Luck #VALUE .5 #COLOR red #ENDBAR
#BAR Charisma #VALUE .7 #COLOR yellow #ENDBAR
#BAR Dexterity #VALUE .9 #COLOR orange #ENDBAR
#BAR Health #VALUE 1 #COLOR green #ENDBAR
#BAR Amps #VALUE amps=="Several" ? 3 : 11 #MAX 11 #COLOR purple #ENDBAR
#SHOW despair #BADGE You are penitent. #IF #ACHIEVED penance #NOW
#SHOW raining #BADGE You are damp. #IF wind==1 #EVER
#SHOW frankenstein-creature #BADGE #INCLUDE messy_hair #NOW
#)
